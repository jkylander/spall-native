package main

import "core:container/queue"
import "core:fmt"
import "core:math"
import "core:runtime"
import "core:slice"
import "core:strings"

next_line :: proc(y: ^f64, h: f64) -> f64 {
	res := y^
	y^ += h + (h / 1.5)
	return res
}
prev_line := proc(y: ^f64, h: f64) -> f64 {
	res := y^
	y^ -= h + (h / 3)
	return res
}

tooltip :: proc(rects: ^[dynamic]DrawRect, pos: Vec2, min_x, max_x: f64, text: string) {
	text_width := measure_text(text, .PSize, .DefaultFont)
	text_height := get_text_height(.PSize, .DefaultFont)

	tooltip_rect := rect(pos.x, pos.y - (em / 2), text_width + em, text_height + (1.25 * em))
	if tooltip_rect.pos.x + tooltip_rect.size.x > max_x {
		tooltip_rect.pos.x = max_x - tooltip_rect.size.x
	}
	if tooltip_rect.pos.x < min_x {
		tooltip_rect.pos.x = min_x
	}

	draw_rect(rects, tooltip_rect, bg_color)
	draw_rect_outline(rects, tooltip_rect, 1, line_color)
	draw_text(rects, text, Vec2{tooltip_rect.pos.x + (em / 2), tooltip_rect.pos.y + (em / 2)}, .PSize, .DefaultFont, text_color)
}

button :: proc(rects: ^[dynamic]DrawRect, in_rect: Rect, label_text, tooltip_text: string, font: FontType, min_x, max_x: f64) -> bool {
	draw_rect(rects, in_rect, toolbar_button_color)
	label_width := measure_text(label_text, .PSize, font)
	label_height := get_text_height(.PSize, font)
	draw_text(rects, label_text, 
		Vec2{
			in_rect.pos.x + (in_rect.size.x / 2) - (label_width / 2), 
			in_rect.pos.y + (in_rect.size.y / 2) - (label_height / 2),
		}, .PSize, font, toolbar_text_color)

	if pt_in_rect(mouse_pos, in_rect) {
		set_cursor("pointer")
		if clicked {
			return true
		} else {
			tip_pos := Vec2{in_rect.pos.x, in_rect.pos.y + in_rect.size.y + em}
			tooltip(rects, tip_pos, min_x, max_x, tooltip_text)
		}
	}
	return false
}

draw_histogram :: proc(rects: ^[dynamic]DrawRect, header: string, stat: ^Stats, pos: Vec2, graph_size: f64) {
	line_width : f64 = 1
	graph_edge_pad : f64 = 2 * em
	line_gap := (em / 1.5)

	history := stat.hist
	temp_history := make([]f64, len(history), context.temp_allocator)

	max_val : f64 = 0
	min_val : f64 = 1e5000
	for entry, i in history {
		temp_history[i] = math.log2_f64(entry + 1)

		max_val = max(max_val, temp_history[i])
		min_val = min(min_val, temp_history[i])
	}
	max_range := max_val - min_val

	graph_top := pos.y + em + line_gap
	graph_bottom := graph_top + graph_size

	graph_y_bounds := graph_size - (graph_edge_pad * 2)
	graph_x_bounds := graph_size - graph_edge_pad

	text_x_overhead := 100.0
	graph_overdraw_rect := rect(pos.x - text_x_overhead, pos.y - line_gap, graph_size + text_x_overhead + (em / 2), ((em + line_gap) * 2) + graph_size + (em / 2) + line_gap)

	// reset mouse if we're in the graph
	if pt_in_rect(mouse_pos, graph_overdraw_rect) {
		rect_tooltip_rect = EventID{-1, -1, -1, -1}
		rect_tooltip_pos = Vec2{}
		rendered_rect_tooltip = false
		reset_cursor()
	}

	draw_rect(rects, graph_overdraw_rect, bg_color)
	draw_rect(rects, rect(pos.x, graph_top, graph_size, graph_size), bg_color2)
	draw_rect_outline(rects, rect(pos.x, graph_top, graph_size, graph_size), 2, outline_color)

	text_width := measure_text(header, .PSize, .DefaultFont)
	center_offset := (graph_size / 2) - (text_width / 2)
	draw_text(rects, header, Vec2{pos.x + center_offset, pos.y}, .PSize, .DefaultFont, text_color)

	high_height := graph_top + graph_edge_pad - (em / 2)
	low_height := graph_bottom - graph_edge_pad - (em / 2)

	near_width := pos.x + (graph_edge_pad / 2)
	far_width  := pos.x + graph_size - (graph_edge_pad / 2)

	if len(temp_history) > 1 {
		buf: [384]byte
		b := strings.builder_from_bytes(buf[:])

		x_tac_count := 5
		for i := 0; i < x_tac_count; i += 1 {
			cur_perc := f64(i) / f64(x_tac_count - 1)
			cur_x_val := math.pow(2, math.lerp(min_val, max_val, cur_perc))
			cur_x_height := math.lerp(low_height, high_height, cur_perc)

			strings.builder_reset(&b)
			my_write_float(&b, cur_x_val, 3)
			cur_x_str := strings.to_string(b)
			cur_x_width := measure_text(cur_x_str, .PSize, .DefaultFont) + line_gap
			draw_text(rects, cur_x_str, Vec2{(pos.x - 5) - cur_x_width, cur_x_height}, .PSize, .DefaultFont, text_color)

			draw_line(rects, Vec2{pos.x - 5, cur_x_height + (em / 2)}, Vec2{pos.x + 5, cur_x_height + (em / 2)}, 1, graph_color)
		}

		y_tac_count := 4
		for i := 0; i < y_tac_count; i += 1 {
			cur_perc := f64(i) / f64(y_tac_count - 1)
			cur_y_val := math.lerp(stat.min_time, stat.max_time, cur_perc)
			cur_y_pos := math.lerp(near_width, far_width, cur_perc)

			cur_y_str := stat_fmt(cur_y_val)
			cur_y_width := measure_text(cur_y_str, .PSize, .DefaultFont)
			draw_text(rects, cur_y_str, Vec2{cur_y_pos - (cur_y_width / 2), graph_bottom + 5}, .PSize, .DefaultFont, text_color)

			draw_line(rects, Vec2{cur_y_pos, graph_bottom - 5}, Vec2{cur_y_pos, graph_bottom + 5}, 1, graph_color)
		}
	}


	last_x : f64 = 0
	last_y : f64 = 0
	for entry, i in temp_history {

		point_x_offset : f64 = 0
		if len(temp_history) != 0 {
			point_x_offset = f64(i) * (graph_x_bounds / f64(len(temp_history)))
		}

		point_y_offset : f64 = 0
		if max_range != 0 {
			point_y_offset = f64(entry - min_val) * (graph_y_bounds / f64(max_range))
		}

		point_x := pos.x + point_x_offset + (graph_edge_pad / 2)
		point_y := graph_top + graph_size - point_y_offset - graph_edge_pad

		if len(temp_history) > 1  && i > 0 {
			draw_line(rects, Vec2{last_x, last_y}, Vec2{point_x, point_y}, line_width, graph_color)
		}

		last_x = point_x
		last_y = point_y
	}

	if len(temp_history) > 1 {
		avg_offset := rescale(stat.avg_time, stat.min_time, stat.max_time, near_width, far_width)
		draw_line(rects, Vec2{avg_offset, graph_top + graph_edge_pad}, Vec2{avg_offset, graph_bottom - graph_edge_pad}, 1, BVec4{255, 0, 0, 255})
	}
}

draw_graph :: proc(rects: ^[dynamic]DrawRect, header: string, history: ^queue.Queue(f64), pos: Vec2) {
	line_width : f64 = 1
	graph_edge_pad : f64 = 2 * em
	line_gap := (em / 1.5)
	graph_size: f64 = 150

	max_val : f64 = 0
	min_val : f64 = 1e5000
	sum_val : f64 = 0
	for i := 0; i < queue.len(history^); i += 1 {
		entry := queue.get(history, i)
		max_val = max(max_val, entry)
		min_val = min(min_val, entry)
		sum_val += entry
	}
	max_range := max_val - min_val
	avg_val := sum_val / 100

	text_width := measure_text(header, .PSize, .DefaultFont)
	center_offset := (graph_size / 2) - (text_width / 2)
	draw_text(rects, header, Vec2{pos.x + center_offset, pos.y}, .PSize, .DefaultFont, text_color)

	graph_top := pos.y + em + line_gap
	draw_rect(rects, rect(pos.x, graph_top, graph_size, graph_size), bg_color2)
	draw_rect_outline(rects, rect(pos.x, graph_top, graph_size, graph_size), 2, outline_color)

	draw_line(rects, Vec2{pos.x - 5, graph_top + graph_size - graph_edge_pad}, Vec2{pos.x + 5, graph_top + graph_size - graph_edge_pad}, 1, graph_color)
	draw_line(rects, Vec2{pos.x - 5, graph_top + graph_edge_pad}, Vec2{pos.x + 5, graph_top + graph_edge_pad}, 1, graph_color)

	if queue.len(history^) > 1 {
		buf: [384]byte
		b := strings.builder_from_bytes(buf[:])

		high_height := graph_top + graph_edge_pad - (em / 2)
		low_height := graph_top + graph_size - graph_edge_pad - (em / 2)
		avg_height := rescale(f64(avg_val), f64(min_val), f64(max_val), low_height, high_height)

		strings.builder_reset(&b)
		my_write_float(&b, max_val, 3)
		high_str := strings.to_string(b)
		high_width := measure_text(high_str, .PSize, .DefaultFont) + line_gap
		draw_text(rects, high_str, Vec2{(pos.x - 5) - high_width, high_height}, .PSize, .DefaultFont, text_color)

		if queue.len(history^) > 90 {
			draw_line(rects, Vec2{pos.x - 5, avg_height + (em / 2)}, Vec2{pos.x + 5, avg_height + (em / 2)}, 1, graph_color)

			strings.builder_reset(&b)
			my_write_float(&b, avg_val, 3)
			avg_str := strings.to_string(b)

			avg_width := measure_text(avg_str, .PSize, .DefaultFont) + line_gap
			draw_text(rects, avg_str, Vec2{(pos.x - 5) - avg_width, avg_height}, .PSize, .DefaultFont, text_color)
		}

		strings.builder_reset(&b)
		my_write_float(&b, min_val, 3)
		low_str := strings.to_string(b)

		low_width := measure_text(low_str, .PSize, .DefaultFont) + line_gap
		draw_text(rects, low_str, Vec2{(pos.x - 5) - low_width, low_height}, .PSize, .DefaultFont, text_color)
	}

	graph_y_bounds := graph_size - (graph_edge_pad * 2)
	graph_x_bounds := graph_size - graph_edge_pad

	last_x : f64 = 0
	last_y : f64 = 0
	for i := 0; i < queue.len(history^); i += 1 {
		entry := queue.get(history, i)

		point_x_offset : f64 = 0
		if queue.len(history^) != 0 {
			point_x_offset = f64(i) * (graph_x_bounds / f64(queue.len(history^)))
		}

		point_y_offset : f64 = 0
		if max_range != 0 {
			point_y_offset = f64(entry - min_val) * (graph_y_bounds / f64(max_range))
		}

		point_x := pos.x + point_x_offset + (graph_edge_pad / 2)
		point_y := graph_top + graph_size - point_y_offset - graph_edge_pad

		if queue.len(history^) > 1  && i > 0 {
			draw_line(rects, Vec2{last_x, last_y}, Vec2{point_x, point_y}, line_width, graph_color)
		}

		last_x = point_x
		last_y = point_y
	}
}

draw_toolbar :: proc(rects: ^[dynamic]DrawRect, trace: ^Trace, toolbar_height, width, display_width: f64) {
	// Render toolbar background
	draw_rect(rects, rect(0, 0, width, toolbar_height), toolbar_color)

	// draw toolbar
	{
		edge_pad := 1 * em
		button_height := 2 * em
		button_width  := 2 * em
		button_pad    := 0.5 * em

		cursor_x := edge_pad

		// Draw Logo
		logo_text := "spall"
		logo_width := measure_text(logo_text, .H1Size, .DefaultFont)
		draw_text(rects, logo_text, Vec2{cursor_x, (toolbar_height / 2) - (h1_height / 2)}, .H1Size, .DefaultFont, toolbar_text_color)
		cursor_x += logo_width + edge_pad

		// Open File
		if button(rects, rect(cursor_x, (toolbar_height / 2) - (button_height / 2), button_width, button_height), "\uf07c", "open file", .IconFont, 0, width) {
			open_file_dialog()
		}
		cursor_x += button_width + button_pad

		// Reset Camera
		if button(rects, rect(cursor_x, (toolbar_height / 2) - (button_height / 2), button_width, button_height), "\uf066", "reset camera", .IconFont, 0, width) {
			reset_camera(trace, display_width)
		}
		cursor_x += button_width + button_pad

		// Process All Events
		if button(rects, rect(cursor_x, (toolbar_height / 2) - (button_height / 2), button_width, button_height), "\uf1fe", "get stats for the whole file", .IconFont, 0, width) {
			stats_state = .Started
			did_multiselect = true
			total_tracked_time = 0.0
			cur_stat_offset = StatOffset{}
			selected_event = {-1, -1, -1, -1}
			info_pane_scroll = 0
			info_pane_scroll_vel = 0

			sm_clear(&trace.stats)
			resize(&trace.selected_ranges, 0)

			for proc_v, p_idx in trace.processes {
				for tm, t_idx in proc_v.threads {
					for depth, d_idx in tm.depths {
						append(&trace.selected_ranges, Range{p_idx, t_idx, d_idx, 0, len(depth.events)})
					}
				}
			}
		}
		cursor_x += button_width + button_pad

		file_name_width := measure_text(trace.base_name, .H1Size, .DefaultFont)
		name_x := max((display_width / 2) - (file_name_width / 2), cursor_x)
		draw_text(rects, trace.base_name, Vec2{name_x, (toolbar_height / 2) - (h1_height / 2)}, .H1Size, .DefaultFont, toolbar_text_color)

		// colormode button nonsense
		color_text : string
		tool_text : string
		switch colormode {
		case .Auto:
			tool_text = "switch to dark colors"
			color_text = "\uf042"
		case .Dark:
			tool_text = "switch to light colors"
			color_text = "\uf10c"
		case .Light:
			tool_text = "switch to auto colors"
			color_text = "\uf111"
		}

		if button(rects, rect(width - edge_pad - button_width, (toolbar_height / 2) - (button_height / 2), button_width, button_height), color_text, tool_text, .IconFont, 0, width) {
			new_colormode: ColorMode

			// rotate between auto, dark, and light
			switch colormode {
			case .Auto:
				new_colormode = .Dark
			case .Dark:
				new_colormode = .Light
			case .Light:
				new_colormode = .Auto
			}

			switch new_colormode {
			case .Auto:
				is_dark := get_system_color()
				set_color_mode(true, is_dark)
				set_session_storage("colormode", "auto")
			case .Dark:
				set_color_mode(false, true)
				set_session_storage("colormode", "dark")
			case .Light:
				set_color_mode(false, false)
				set_session_storage("colormode", "light")
			}
			colormode = new_colormode
		}
		if button(rects, rect(width - edge_pad - ((button_width * 2) + (button_pad)), (toolbar_height / 2) - (button_height / 2), button_width, button_height), "\uf188", "toggle debug mode", .IconFont, 0, width) {
			enable_debug = !enable_debug
		}
	}
}

draw_debug :: proc(rects: ^[dynamic]DrawRect, width, text_y, x_subpad: f64, graph_pos: Vec2) {
	y := text_y
	draw_graph(rects, "FPS", &fps_history, graph_pos)

	hash_str := fmt.tprintf("Build: 0x%X", abs(build_hash))
	hash_width := measure_text(hash_str, .PSize, .MonoFont)
	draw_text(rects, hash_str, Vec2{width - hash_width - x_subpad, prev_line(&y, em)}, .PSize, .MonoFont, text_color2)

	seed_str := fmt.tprintf("Seed: 0x%X", random_seed)
	seed_width := measure_text(seed_str, .PSize, .MonoFont)
	draw_text(rects, seed_str, Vec2{width - seed_width - x_subpad, prev_line(&y, em)}, .PSize, .MonoFont, text_color2)

	rects_str := fmt.tprintf("Rect Count: %d", rect_count)
	rects_txt_width := measure_text(rects_str, .PSize, .MonoFont)
	draw_text(rects, rects_str, Vec2{width - rects_txt_width - x_subpad, prev_line(&y, em)}, .PSize, .MonoFont, text_color2)

	buckets_str := fmt.tprintf("Bucket Count: %d", bucket_count)
	buckets_txt_width := measure_text(buckets_str, .PSize, .MonoFont)
	draw_text(rects, buckets_str, Vec2{width - buckets_txt_width - x_subpad, prev_line(&y, em)}, .PSize, .MonoFont, text_color2)

	events_str := fmt.tprintf("Event Count: %d", rect_count - bucket_count)
	events_txt_width := measure_text(events_str, .PSize, .MonoFont)
	draw_text(rects, events_str, Vec2{width - events_txt_width - x_subpad, prev_line(&y, em)}, .PSize, .MonoFont, text_color2)

	cache_hit_str := fmt.tprintf("TTF Cache Hits: %d", cache_hits_this_frame)
	cache_hit_txt_width := measure_text(cache_hit_str, .PSize, .MonoFont)
	draw_text(rects, cache_hit_str, Vec2{width - cache_hit_txt_width - x_subpad, prev_line(&y, em)}, .PSize, .MonoFont, text_color2)

	cache_miss_str := fmt.tprintf("TTF Cache Misses: %d", cache_misses_this_frame)
	cache_miss_txt_width := measure_text(cache_miss_str, .PSize, .MonoFont)
	draw_text(rects, cache_miss_str, Vec2{width - cache_miss_txt_width - x_subpad, prev_line(&y, em)}, .PSize, .MonoFont, text_color2)

	cache_hits_this_frame = 0
	cache_misses_this_frame = 0
}

draw_rect_tooltip :: proc(rects: ^[dynamic]DrawRect, trace: ^Trace, dpr: f64) {
	tip_pos := mouse_pos
	tip_pos += Vec2{1, 2} * em / dpr

	ids := rect_tooltip_rect
	thread := trace.processes[ids.pid].threads[ids.tid]
	depth := thread.depths[ids.did]
	ev := depth.events[ids.eid]

	duration := bound_duration(ev, thread.max_time)

	rect_tooltip_name := in_getstr(&trace.string_block, ev.name)
	if ev.duration == -1 {
		rect_tooltip_name = fmt.tprintf("%s (Did Not Finish)", in_getstr(&trace.string_block, ev.name))
	}

	rect_tooltip_stats: string
	if ev.self_time != 0 && ev.self_time != duration {
		rect_tooltip_stats = fmt.tprintf("%s (self %s)", tooltip_fmt(duration), tooltip_fmt(ev.self_time))
	} else {
		rect_tooltip_stats = tooltip_fmt(duration)
	}

	text_height := get_text_height(.PSize, .DefaultFont)
	name_width := measure_text(rect_tooltip_name, .PSize, .DefaultFont)
	stats_width := measure_text(rect_tooltip_stats, .PSize, .DefaultFont)

	args := in_getstr(&trace.string_block, ev.args)
	args_width := measure_text(args, .PSize, .DefaultFont)

	rect_width := max(name_width + em + stats_width + em, args_width + em)
	rect_height := text_height + (1.25 * em)
	if len(args) > 0 {
		next_line(&rect_height, em)
	}

	tooltip_rect := rect(tip_pos.x, tip_pos.y - (em / 2), rect_width, rect_height)

	min_x := graph_rect.pos.x
	max_x := graph_rect.pos.x + graph_rect.size.x
	if tooltip_rect.pos.x + tooltip_rect.size.x > max_x {
		tooltip_rect.pos.x = max_x - tooltip_rect.size.x
	}
	if tooltip_rect.pos.x < min_x {
		tooltip_rect.pos.x = min_x
	}

	draw_rect(rects, tooltip_rect, bg_color)
	draw_rect_outline(rects, tooltip_rect, 1, line_color)
	tooltip_start_x := tooltip_rect.pos.x + (em / 2)
	tooltip_start_y := tooltip_rect.pos.y + (em / 2)

	cursor_x := tooltip_start_x
	cursor_y := tooltip_start_y

	draw_text(rects, rect_tooltip_stats, Vec2{cursor_x, cursor_y}, .PSize, .DefaultFont, rect_tooltip_stats_color)
	cursor_x += (em * 0.35) + stats_width
	draw_text(rects, rect_tooltip_name, Vec2{cursor_x, cursor_y}, .PSize, .DefaultFont, text_color)

	if len(args) > 0 {
		next_line(&cursor_y, em)
		draw_text(rects, args, Vec2{tooltip_start_x, cursor_y}, .PSize, .DefaultFont, text_color)
	}
}

render_widetree :: proc(rects: ^[dynamic]DrawRect, trace: ^Trace, p_idx, t_idx: int, start_x, y, height, scale: f64, layer_count: int) {
	thread := &trace.processes[p_idx].threads[t_idx]
	depth := thread.depths[0]
	tree := depth.tree

	// If we blow this, we're in space
	tree_stack := [128]uint{}
	stack_len := 0

	alpha := u8(255.0 / f64(layer_count))
	tree_stack[0] = depth.head; stack_len += 1
	for stack_len > 0 {
		stack_len -= 1

		tree_idx := tree_stack[stack_len]
		if tree_idx >= len(tree) {
			fmt.printf("%d, %d\n", p_idx, t_idx)
			fmt.printf("%d\n", depth.head)
			fmt.printf("%d\n", stack_len)
			fmt.printf("%v\n", tree_stack)
			fmt.printf("%v\n", tree)
			fmt.printf("hmm????\n")
			push_fatal(SpallError.Bug)
		}

		cur_node := tree[tree_idx]
		range := cur_node.end_time - cur_node.start_time
		range_width := range * scale

		// draw summary faketangle
		min_width := 2.0 
		if (range_width / math.sqrt_f64(CHUNK_NARY_WIDTH)) < min_width {
			x := cur_node.start_time
			w := min_width * math.sqrt_f64(CHUNK_NARY_WIDTH)
			xm := x * scale

			r_x   := x * scale
			end_x := r_x + w

			r_x   += start_x
			end_x += start_x

			r_x    = max(r_x, 0)
			r_w   := end_x - r_x

			draw_rect(rects, rect(r_x, y, r_w, height), BVec4{u8(wide_rect_color.x), u8(wide_rect_color.y), u8(wide_rect_color.z), alpha})
			continue
		}

		// we're at a bottom node, draw the whole thing
		if cur_node.child_count == 0 {
			scan_arr := depth.events[cur_node.start_idx:cur_node.start_idx+uint(cur_node.arr_len)]
			render_wideevents(rects, trace, scan_arr, thread.max_time, start_x, y, height, scale, alpha)
			continue
		}

		for i := cur_node.child_count - 1; i >= 0; i -= 1 {
			tree_stack[stack_len] = cur_node.children[i]; stack_len += 1
		}
	}
}

render_wideevents :: proc(rects: ^[dynamic]DrawRect, trace: ^Trace, scan_arr: []Event, thread_max_time: f64, start_x, y, height, scale: f64, alpha: u8) {
	for ev, de_id in scan_arr {
		x := ev.timestamp - trace.total_min_time
		duration := bound_duration(ev, thread_max_time)
		w := max(duration * scale, 2.0)
		xm := x * scale

		// Carefully extract the [start, end] interval of the rect so that we can clip the left
		// side to 0 before sending it to draw_rect, so we can prevent f32 (f64?) precision
		// problems drawing a rectangle which starts at a massively huge negative number on
		// the left.
		r_x   := x * scale
		end_x := r_x + w

		r_x   += start_x
		end_x += start_x

		r_x    = max(r_x, 0)
		r_w   := end_x - r_x

		draw_rect(rects, rect(r_x, y, r_w, height), BVec4{u8(wide_rect_color.x), u8(wide_rect_color.y), u8(wide_rect_color.z), alpha})
	}
}

render_minitree :: proc(rects: ^[dynamic]DrawRect, trace: ^Trace, pid, tid: int, did: int, start_x, y, height, scale: f64) {
	thread := trace.processes[pid].threads[tid]
	depth := thread.depths[did]
	tree := depth.tree

	if len(tree) == 0 {
		fmt.printf("depth_idx: %d, depth count: %d, %v\n", did, len(thread.depths), thread.depths)
		push_fatal(SpallError.Bug)
	}

	found_rid := -1
	range_loop: for range, r_idx in trace.selected_ranges {
		if range.pid == pid && range.tid == tid && range.did == did {
			found_rid = r_idx
			break
		}
	}

	// If we blow this, we're in space
	tree_stack := [128]uint{}
	stack_len := 0

	tree_stack[0] = depth.head; stack_len += 1
	for stack_len > 0 {
		stack_len -= 1

		tree_idx := tree_stack[stack_len]
		cur_node := tree[tree_idx]
		range := cur_node.end_time - cur_node.start_time
		range_width := range * scale

		// draw summary faketangle
		min_width := 2.0 
		if (range_width / math.sqrt_f64(CHUNK_NARY_WIDTH)) < min_width {
			x := cur_node.start_time
			w := min_width * math.sqrt_f64(CHUNK_NARY_WIDTH)
			xm := x * scale

			r_x   := x * scale
			end_x := r_x + w

			r_x   += start_x
			end_x += start_x

			r_x    = max(r_x, 0)
			r_w   := end_x - r_x

			rect_color := cur_node.avg_color
			grey := greyscale(cur_node.avg_color)
			should_fade := false
			if did_multiselect {
				if found_rid == -1 { should_fade = true } 
				else {
					range := trace.selected_ranges[found_rid]	
					if !range_in_range(cur_node.start_idx, cur_node.end_idx, 
									   uint(range.start), uint(range.end)) {
						should_fade = true
					}
				}
			}
			if should_fade {
				if multiselect_t != 0 && greyanim_t > 1 {
					anim_playing = false
					rect_color = grey
				} else {
					st := ease_in_out(greyanim_t)
					rect_color = math.lerp(rect_color, grey, greymotion)
				}
			}

			draw_rect(rects, rect(r_x, y, r_w, height), BVec4{u8(rect_color.x), u8(rect_color.y), u8(rect_color.z), 255})
			continue
		}

		// we're at a bottom node, draw the whole thing
		if cur_node.child_count == 0 {
			scan_arr := depth.events[cur_node.start_idx:cur_node.start_idx+uint(cur_node.arr_len)]
			render_minievents(rects, trace, scan_arr, thread.max_time, start_x, y, height, scale, int(cur_node.start_idx), found_rid)
			continue
		}

		for i := cur_node.child_count - 1; i >= 0; i -= 1 {
			tree_stack[stack_len] = cur_node.children[i]; stack_len += 1
		}
	}
}

render_minievents :: proc(rects: ^[dynamic]DrawRect, trace: ^Trace, scan_arr: []Event, thread_max_time: f64, start_x, y, height, scale: f64, start_idx, found_rid: int) {
	for ev, de_id in scan_arr {
		x := ev.timestamp - trace.total_min_time
		duration := bound_duration(ev, thread_max_time)
		w := max(duration * scale, 2.0)
		xm := x * scale

		// Carefully extract the [start, end] interval of the rect so that we can clip the left
		// side to 0 before sending it to draw_rect, so we can prevent f32 (f64?) precision
		// problems drawing a rectangle which starts at a massively huge negative number on
		// the left.
		r_x   := x * scale
		end_x := r_x + w

		r_x   += start_x
		end_x += start_x

		r_x    = max(r_x, 0)
		r_w   := end_x - r_x

		idx := name_color_idx(trace, in_getstr(&trace.string_block, ev.name))
		rect_color := trace.color_choices[idx]
		e_idx := int(start_idx) + de_id

		grey := greyscale(trace.color_choices[idx])
		should_fade := false
		if did_multiselect {
			if found_rid == -1 { should_fade = true } 
			else {
				range := trace.selected_ranges[found_rid]	
				if !val_in_range(e_idx, range.start, range.end - 1) { should_fade = true }
			}
		}

		if should_fade {
			if multiselect_t != 0 && greyanim_t > 1 {
				anim_playing = false
				rect_color = grey
			} else {
				st := ease_in_out(greyanim_t)
				rect_color = math.lerp(rect_color, grey, greymotion)
			}
		}

		draw_rect(rects, rect(r_x, y, r_w, height), BVec4{u8(rect_color.x), u8(rect_color.y), u8(rect_color.z), 255})
	}
}

render_tree :: proc(rects: ^[dynamic]DrawRect, text_rects: ^[dynamic]TextRect, trace: ^Trace, pid, tid, did: int, y_start, height, start_time, end_time: f64) {
	thread := trace.processes[pid].threads[tid]
	depth := thread.depths[did]
	tree := depth.tree

	found_rid := -1
	range_loop: for range, r_idx in trace.selected_ranges {
		if range.pid == pid && range.tid == tid && range.did == did {
			found_rid = r_idx
			break
		}
	}

	// If we blow this, we're in space
	tree_stack := [128]uint{}
	stack_len := 0

	tree_stack[0] = depth.head; stack_len += 1
	for stack_len > 0 {
		stack_len -= 1

		tree_idx := tree_stack[stack_len]
		cur_node := tree[tree_idx]

		if cur_node.end_time < f64(start_time) || cur_node.start_time > f64(end_time) {
			continue
		}

		range := cur_node.end_time - cur_node.start_time
		range_width := range * cam.current_scale

		// draw summary faketangle
		min_width := 2.0
		if (range_width / math.sqrt_f64(CHUNK_NARY_WIDTH)) < min_width {
			y := height * f64(did)
			h := height

			x := cur_node.start_time
			w := min_width * math.sqrt_f64(CHUNK_NARY_WIDTH)
			xm := x * cam.target_scale

			r_x   := x * cam.current_scale
			end_x := r_x + w

			r_x   += cam.pan.x + disp_rect.pos.x
			end_x += cam.pan.x + disp_rect.pos.x

			r_x    = max(r_x, 0)

			r_y := y_start + y
			dr := Rect{Vec2{r_x, r_y}, Vec2{end_x - r_x, h}}

			rect_color := cur_node.avg_color

			grey := greyscale(cur_node.avg_color)
			should_fade := false
			if did_multiselect {
				if found_rid == -1 { should_fade = true } 
				else {
					range := trace.selected_ranges[found_rid]	
					if !range_in_range(cur_node.start_idx, cur_node.end_idx, 
									   uint(range.start), uint(range.end)) {
						should_fade = true
					}
				}
			}
			if should_fade {
				if multiselect_t != 0 && greyanim_t > 1 {
					anim_playing = false
					rect_color = grey
				} else {
					st := ease_in_out(greyanim_t)
					rect_color = math.lerp(rect_color, grey, greymotion)
				}
			}

			draw_rect(rects, dr, BVec4{u8(rect_color.x), u8(rect_color.y), u8(rect_color.z), 255})

			rect_count += 1
			bucket_count += 1
			continue
		}

		// we're at a bottom node, draw the whole thing
		if cur_node.child_count == 0 {
			render_events(rects, text_rects, trace, 
				pid, tid, did, depth.events[:], cur_node.start_idx, cur_node.arr_len, y_start, height, found_rid)
			continue
		}

		for i := cur_node.child_count - 1; i >= 0; i -= 1 {
			tree_stack[stack_len] = cur_node.children[i]; stack_len += 1
		}
	}
}

render_events :: proc(rects: ^[dynamic]DrawRect, text_rects: ^[dynamic]TextRect, trace: ^Trace, p_idx, t_idx, d_idx: int, events: []Event, start_idx: uint, arr_len: i8, y_start, height: f64, found_rid: int) {
	thread := trace.processes[p_idx].threads[t_idx]
	scan_arr := events[start_idx:start_idx+uint(arr_len)]
	y := height * f64(d_idx)
	h := height

	for ev, de_id in scan_arr {
		x := ev.timestamp - trace.total_min_time
		duration := bound_duration(ev, thread.max_time)
		w := max(duration * cam.current_scale, 2.0)
		xm := x * cam.target_scale


		// Carefully extract the [start, end] interval of the rect so that we can clip the left
		// side to 0 before sending it to draw_rect, so we can prevent f32 (f64?) precision
		// problems drawing a rectangle which starts at a massively huge negative number on
		// the left.
		r_x   := x * cam.current_scale
		end_x := r_x + w

		r_x   += cam.pan.x + disp_rect.pos.x
		end_x += cam.pan.x + disp_rect.pos.x

		r_x    = max(r_x, 0)

		r_y := y_start + y
		dr := Rect{Vec2{r_x, r_y}, Vec2{end_x - r_x, h}}

		if !rect_in_rect(dr, graph_rect) {
			continue
		}

		ev_name := in_getstr(&trace.string_block, ev.name)
		idx := name_color_idx(trace, ev_name)
		rect_color := trace.color_choices[idx]
		e_idx := int(start_idx) + de_id

		grey := greyscale(trace.color_choices[idx])

		should_fade := false
		if did_multiselect {
			if found_rid == -1 { should_fade = true } 
			else {
				range := trace.selected_ranges[found_rid]	
				if !val_in_range(e_idx, range.start, range.end - 1) { should_fade = true }
			}
		}

		if should_fade {
			if multiselect_t != 0 && greyanim_t > 1 {
				anim_playing = false
				rect_color = grey
			} else {
				rect_color = math.lerp(rect_color, grey, greymotion)
			}
		}

		if int(selected_event.pid) == p_idx && int(selected_event.tid) == t_idx &&
		   int(selected_event.did) == d_idx && int(selected_event.eid) == e_idx {
			rect_color.x += 30
			rect_color.y += 30
			rect_color.z += 30
		}

		draw_rect(rects, dr, BVec4{u8(rect_color.x), u8(rect_color.y), u8(rect_color.z), 255})
		rect_count += 1

		underhang := disp_rect.pos.x - dr.pos.x
		overhang := (disp_rect.pos.x + disp_rect.size.x) - dr.pos.x
		disp_w := min(dr.size.x - underhang, dr.size.x, overhang)

		display_name := ev_name
		if ev.duration == -1 {
			display_name = fmt.tprintf("%s (Did Not Finish)", ev_name)
		}
		text_pad := (em / 2)
		text_width := int(math.floor((disp_w - (text_pad * 2)) / ch_width))
		max_chars := max(0, min(len(display_name), text_width))
		name_str := display_name[:max_chars]
		str_x := max(dr.pos.x, disp_rect.pos.x) + text_pad

		if len(name_str) > 4 || max_chars == len(display_name) {
			if max_chars != len(display_name) {
				name_str = fmt.tprintf("%s…", name_str[:len(name_str)-1])
			}

			batch_text(text_rects, name_str, Vec2{str_x, dr.pos.y + (height / 2) - (em / 2)}, .PSize, .MonoFont, text_color3)
		}

		if pt_in_rect(mouse_pos, graph_rect) && pt_in_rect(mouse_pos, dr) {
			set_cursor("pointer")
			if !rendered_rect_tooltip && !shift_down {
				rect_tooltip_pos = dr.pos
				rect_tooltip_rect = {i64(p_idx), i64(t_idx), i64(d_idx), i64(e_idx)}
				rendered_rect_tooltip = true
			}

			if clicked && !shift_down {
				pressed_event = {i64(p_idx), i64(t_idx), i64(d_idx), i64(e_idx)}
			}
			if mouse_up_now && !shift_down {
				released_event = {i64(p_idx), i64(t_idx), i64(d_idx), i64(e_idx)}
			}
		}
	}
}

draw_flamegraphs :: proc(rects: ^[dynamic]DrawRect, text_rects: ^[dynamic]TextRect, trace: ^Trace, start_time, end_time, start_x, rect_height, info_pane_y, graph_header_height, graph_header_text_height, top_line_gap, display_width: f64) {
	// graph-relative timebar and subdivisions
	division, draw_tick_start: f64
	ticks: int
	{
		// mus_range := end_time - start_time <- simplifies to the following
		mus_range := display_width / cam.current_scale
		v1 := math.log10(mus_range)
		v2 := math.floor(v1)
		rem := v1 - v2

		division = math.pow(10, v2)                        // multiples of 10
		if rem < 0.3      { division -= (division * 0.8) } // multiples of 2
		else if rem < 0.6 { division -= (division / 2)   } // multiples of 5

		display_range_start := -cam.pan.x / cam.current_scale
		display_range_end := (display_width - cam.pan.x) / cam.current_scale

		draw_tick_start = f_round_down(display_range_start, division)
		draw_tick_end := f_round_down(display_range_end, division)
		tick_range := draw_tick_end - draw_tick_start

		ticks = int(tick_range / division) + 3

		subdivisions := 5
		line_x_start := -4
		line_x_end   := ticks * subdivisions

		line_start := disp_rect.pos.y + graph_header_height - top_line_gap
		line_height := graph_rect.size.y
		for i := line_x_start; i < line_x_end; i += 1 {
			tick_time := draw_tick_start + (f64(i) * (division / f64(subdivisions)))
			x_off := (tick_time * cam.current_scale) + cam.pan.x

			color := (i % subdivisions) != 0 ? subdivision_color : division_color

			draw_line(rects, Vec2{start_x + x_off, line_start}, Vec2{start_x + x_off, line_start + line_height}, 1, BVec4{u8(color.x), u8(color.y), u8(color.z), u8(color.w)})
		}

	}
	flush_rects(rects)

	// graph
	cur_y := padded_graph_rect.pos.y - cam.pan.y
	proc_loop: for proc_v, p_idx in &trace.processes {
		h1_size : f64 = 0
		if len(trace.processes) > 1 {
			if cur_y > disp_rect.pos.y {
				row_text: string
				if proc_v.name.len > 0 {
					row_text = fmt.tprintf("%s (PID %d)", in_getstr(&trace.string_block, proc_v.name), proc_v.process_id)
				} else {
					row_text = fmt.tprintf("PID: %d", proc_v.process_id)
				}
				batch_text(text_rects, row_text, Vec2{start_x + 5, cur_y}, .H1Size, .DefaultFont, text_color)
			}

			h1_size = h1_height + (h1_height / 2)
			cur_y += h1_size
		}

		thread_loop: for tm, t_idx in &proc_v.threads {
			last_cur_y := cur_y
			h2_size := h2_height + (h2_height / 2)
			cur_y += h2_size

			thread_gap := 8.0
			thread_advance := ((f64(len(tm.depths)) * rect_height) + thread_gap)

			if cur_y > info_pane_y {
				break proc_loop
			}
			if cur_y + thread_advance < 0 {
				cur_y += thread_advance
				continue
			}

			if last_cur_y > disp_rect.pos.y {
				row_text: string
				if tm.name.len > 0 {
					row_text = fmt.tprintf("%s (TID %d)", in_getstr(&trace.string_block, tm.name), tm.thread_id)
				} else {
					row_text = fmt.tprintf("TID: %d", tm.thread_id)
				}
				batch_text(text_rects, row_text, Vec2{start_x + 5, last_cur_y}, .H2Size, .DefaultFont, text_color)
			}

			cur_depth_off := 0
			for depth, d_idx in &tm.depths {
				render_tree(rects, text_rects, trace, p_idx, t_idx, d_idx, cur_y, rect_height, start_time, end_time)
			}
			cur_y += thread_advance
		}
	}
	flush_rects(rects)
	flush_text_batch(text_rects)

	// relative time back-cover
	draw_rect(rects, rect(start_x, disp_rect.pos.y, display_width, graph_header_text_height), bg_color)

	// timestamps for subdivision lines
	for i := 0; i < ticks; i += 1 {
		tick_time := draw_tick_start + (f64(i) * division)
		x_off := (tick_time * cam.current_scale) + cam.pan.x

		time_str := time_fmt(tick_time)
		text_width := measure_text(time_str, .PSize, .DefaultFont)
		draw_text(rects, time_str, Vec2{start_x + x_off - (text_width / 2), disp_rect.pos.y + (graph_header_text_height / 2) - (em / 3)}, .PSize, .DefaultFont, text_color)
	}
}

draw_widegraph :: proc(rects: ^[dynamic]DrawRect, trace: ^Trace, highlight_start_x, highlight_end_x, start_x, display_width, wide_graph_height, width, wide_graph_y, mini_graph_padded_width: f64) {
	wide_scale_x := rescale(1.0, 0, trace.total_max_time - trace.total_min_time, 0, display_width)
	layer_count := 1
	for proc_v, _ in trace.processes {
		layer_count += len(proc_v.threads)
	}

	draw_rect(rects, rect(start_x, wide_graph_y, display_width, wide_graph_height), BVec4{u8(wide_bg_color.x), u8(wide_bg_color.y), u8(wide_bg_color.z), u8(wide_bg_color.w)})

	for proc_v, p_idx in &trace.processes {
		for tm, t_idx in &proc_v.threads {
			if len(tm.depths) == 0 {
				continue
			}

			render_widetree(rects, trace, p_idx, t_idx, start_x, wide_graph_y, wide_graph_height, wide_scale_x, layer_count)
		}
	}

	highlight_box_l := rect(start_x, wide_graph_y, highlight_start_x, wide_graph_height)
	draw_rect(rects, highlight_box_l, BVec4{0, 0, 0, 150})

	highlight_box_r := rect(start_x + highlight_end_x, wide_graph_y, display_width - highlight_end_x, wide_graph_height)
	draw_rect(rects, highlight_box_r, BVec4{0, 0, 0, 150})

	draw_rect(rects, rect(0, wide_graph_y, start_x, wide_graph_height), BVec4{0, 0, 0, 255})
	draw_rect(rects, rect(width - mini_graph_padded_width, wide_graph_y, mini_graph_padded_width, wide_graph_height), BVec4{0, 0, 0, 255})
}

draw_minimap :: proc(rects: ^[dynamic]DrawRect, trace: ^Trace, rect_height, mini_graph_width, display_height, mini_start_x, mini_graph_pad, mini_graph_padded_width, graph_header_text_height: f64) {
	// draw back-covers
	draw_rect(rects, rect(mini_start_x, disp_rect.pos.y, mini_graph_width + (mini_graph_pad * 2), display_height), bg_color)

	mini_rect_height := (em / 2)
	mini_thread_gap := 8.0
	x_scale := rescale(1.0, 0, trace.total_max_time - trace.total_min_time, 0, mini_graph_width)
	y_scale := mini_rect_height / rect_height

	tree_y : f64 = padded_graph_rect.pos.y - (cam.pan.y * y_scale)
	for proc_v, p_idx in &trace.processes {
		for tm, t_idx in &proc_v.threads {
			for depth, d_idx in &tm.depths {
				render_minitree(rects, trace, p_idx, t_idx, d_idx, mini_start_x + mini_graph_pad, (tree_y + (mini_rect_height * f64(d_idx))), mini_rect_height, x_scale)
			}

			tree_y += ((f64(len(tm.depths)) * mini_rect_height) + mini_thread_gap)
		}
	}

	preview_height := display_height * y_scale

	// alpha overlays
	draw_rect(rects, rect(mini_start_x, disp_rect.pos.y, mini_graph_padded_width, preview_height), highlight_color)
	draw_rect(rects, rect(mini_start_x, disp_rect.pos.y + preview_height, mini_graph_padded_width, display_height - preview_height), shadow_color)

	// top-right cover-chunk
	draw_rect(rects, rect(mini_start_x, disp_rect.pos.y, mini_graph_width + (mini_graph_pad * 2), graph_header_text_height), bg_color)
}

draw_topbars :: proc(rects: ^[dynamic]DrawRect, trace: ^Trace, width, height, display_width, graph_header_height, top_line_gap, start_x, toolbar_height, graph_header_text_height, time_bar_height, wide_graph_height, wide_graph_y, mini_graph_padded_width, start_time, end_time: f64) {
	// draw back-covers
	draw_rect(rects, rect(0, toolbar_height, width, time_bar_height + wide_graph_height), bg_color) // top
	draw_rect(rects, rect(0, toolbar_height, start_x, height), bg_color) // left

	draw_line(rects, Vec2{start_x, disp_rect.pos.y + graph_header_text_height}, 
					  Vec2{width - mini_graph_padded_width, disp_rect.pos.y + graph_header_text_height}, 1, line_color)

	highlight_start_x := rescale(start_time, 0, trace.total_max_time - trace.total_min_time, 0, display_width)
	highlight_end_x := rescale(end_time, 0, trace.total_max_time - trace.total_min_time, 0, display_width)
	highlight_width := highlight_end_x - highlight_start_x
	min_highlight := 5.0
	if highlight_width < min_highlight {
		high_center := (highlight_start_x + highlight_end_x) / 2
		highlight_start_x = high_center - (min_highlight / 2)
		highlight_end_x = high_center + (min_highlight / 2)
	}
	draw_widegraph(rects, trace, highlight_start_x, highlight_end_x, start_x, display_width, wide_graph_height, width, wide_graph_y, mini_graph_padded_width)

	// global timebar
	{
		start_time : f64 = 0
		end_time   := trace.total_max_time - trace.total_min_time
		default_scale := rescale(1.0, start_time, end_time, 0, display_width)

		mus_range := display_width / default_scale
		v1 := math.log10(mus_range)
		v2 := math.floor(v1)
		rem := v1 - v2

		subdivisions := 10
		division := math.pow(10, v2); // multiples of 10
		if rem < 0.3      { division -= (division * 0.8); } // multiples of 2
		else if rem < 0.6 { division -= (division / 2); } // multiples of 5

		display_range_start := -width / default_scale
		display_range_end := width / default_scale

		draw_tick_start := f_round_down(display_range_start, division)
		draw_tick_end := f_round_down(display_range_end, division)
		tick_range := draw_tick_end - draw_tick_start

		division /= f64(subdivisions)
		ticks := (int(tick_range / division) + 1)

		for i := 0; i < ticks; i += 1 {
			tick_time := draw_tick_start + (f64(i) * division)
			x_off := (tick_time * default_scale)

			line_start_y: f64
			if (i % subdivisions) == 0 {
				time_str := time_fmt(tick_time)
				text_width := measure_text(time_str, .PSize, .DefaultFont)

				draw_text(rects, time_str, 
					Vec2{
						start_x + x_off - (text_width / 2),
						toolbar_height + (time_bar_height / 2) - (em / 2),
					}, .PSize, .DefaultFont, text_color)
				line_start_y = toolbar_height + (time_bar_height / 2) - (em / 2) + p_height
			} else {
				line_start_y = toolbar_height + (time_bar_height / 2) - (em / 2) + p_height + (p_height / 6)
			}

			draw_line(rects,
				Vec2{start_x + x_off, line_start_y}, 
				Vec2{start_x + x_off, toolbar_height + time_bar_height - 2}, 2, division_color)
		}

		draw_line(rects, Vec2{start_x + highlight_start_x, toolbar_height + (time_bar_height / 2) - (em / 2) + p_height}, Vec2{start_x + highlight_start_x, toolbar_height + time_bar_height + wide_graph_height}, 2, xbar_color)
		draw_line(rects, Vec2{start_x + highlight_end_x, toolbar_height + (time_bar_height / 2) - (em / 2) + p_height}, Vec2{start_x + highlight_end_x, toolbar_height + time_bar_height + wide_graph_height}, 2, xbar_color)
		draw_line(rects, Vec2{0, toolbar_height + time_bar_height + wide_graph_height}, Vec2{width, toolbar_height + time_bar_height + wide_graph_height}, 1, line_color)
	}
}

INITIAL_ITER :: 500_000
FULL_ITER    :: 2_000_000
draw_stats :: proc(rects: ^[dynamic]DrawRect, trace: ^Trace, info_pane_y, info_pane_height, top_line_gap, x_subpad, width, height, display_width: f64, info_line_count: int, just_started: bool) {
	// Render info pane back-covers
	draw_line(rects, Vec2{0, info_pane_y}, Vec2{width, info_pane_y}, 1, line_color)
	draw_rect(rects, rect(0, info_pane_y, width, height), bg_color) // bottom

	// If the user selected a single rectangle
	if selected_event.pid != -1 && selected_event.tid != -1 && selected_event.did != -1 && selected_event.eid != -1 {
		p_idx := int(selected_event.pid)
		t_idx := int(selected_event.tid)
		d_idx := int(selected_event.did)
		e_idx := int(selected_event.eid)

		y := info_pane_y + top_line_gap

		thread := trace.processes[p_idx].threads[t_idx]
		event := thread.depths[d_idx].events[e_idx]
		draw_text(rects, in_getstr(&trace.string_block, event.name), Vec2{x_subpad, next_line(&y, em)}, .PSize, .MonoFont, text_color)
		if event.args.len > 0 {
			draw_text(rects, fmt.tprintf(" user data: %s", in_getstr(&trace.string_block, event.args)), Vec2{x_subpad, next_line(&y, em)}, .PSize, .MonoFont, text_color)
		}
		draw_text(rects, fmt.tprintf("start time:%s", time_fmt(event.timestamp - trace.total_min_time)), Vec2{x_subpad, next_line(&y, em)}, .PSize, .MonoFont, text_color)
		draw_text(rects, fmt.tprintf("  duration:%s", time_fmt(bound_duration(event, thread.max_time))), Vec2{x_subpad, next_line(&y, em)}, .PSize, .MonoFont, text_color)
		draw_text(rects, fmt.tprintf(" self time:%s", time_fmt(event.self_time)), Vec2{x_subpad, next_line(&y, em)}, .PSize, .MonoFont, text_color)

	// If we've got stats cooking already
	} else if stats_state == .Started {
		y := info_pane_y + top_line_gap
		center_x := width / 2
		
		total_count := 0
		cur_count := 0
		for range, r_idx in trace.selected_ranges {
			thread := trace.processes[range.pid].threads[range.tid]
			events := thread.depths[range.did].events

			total_count += len(events)
			if cur_stat_offset.range_idx > r_idx {
				cur_count += len(events)
			} else if cur_stat_offset.range_idx == r_idx {
				cur_count += cur_stat_offset.event_idx - range.start
			}
		}


		loading_str := "Stats loading..."
		progress_str := fmt.tprintf("%d of %d", cur_count, total_count)
		hint_str := "Release multi-select to get the rest of the stats"

		strs := []string{ loading_str, progress_str }
		if just_started && total_count >= INITIAL_ITER {
			strs = []string{ loading_str, progress_str, hint_str }
		}

		max_height := 0.0
		for str in strs {
			next_line(&max_height, em)
		}

		cur_y := y + ((height - y) / 2) - (max_height / 2)
		for str in strs {
			str_width := measure_text(str, .PSize, .DefaultFont)
			draw_text(rects, str, Vec2{center_x - (str_width / 2), next_line(&cur_y, em)}, .PSize, .DefaultFont, text_color)
		}

	// If stats are ready to display
	} else if stats_state == .Finished && did_multiselect {
		y := info_pane_y + top_line_gap

		header_start := y
		header_height := 2 * em

		column_gap := 1.5 * em

		cursor := x_subpad

		text_outf :: proc(rects: ^[dynamic]DrawRect, cursor: ^f64, y: f64, str: string, color := text_color) {
			width := measure_text(str, .PSize, .MonoFont)
			draw_text(rects, str, Vec2{cursor^, y}, .PSize, .MonoFont, color)
			cursor^ += width
		}

		full_time := trace.total_max_time - trace.total_min_time

		y += header_height + (em / 4)

		displayed_lines := info_line_count - 1
		if displayed_lines < len(trace.stats.entries) {
			max_lines := len(trace.stats.entries)

			// goofy hack to get line height
			tmp := y
			next_line(&tmp, em)
			line_height := tmp - y

			max_scroll := (f64(max_lines - displayed_lines) * line_height) + (em / 4)
			info_pane_scroll = max(info_pane_scroll, -max_scroll)
			y += info_pane_scroll
		}

		stat_idx := 0
		last_pos := 0.0
		stat_loop: for i := 0; i < len(trace.stats.entries); i += 1 {
			entry := trace.stats.entries[i]
			name := entry.key
			stat := entry.val

			stat_idx += 1
			if y < (info_pane_y + (em / 2)) {
				next_line(&y, em)
				continue stat_loop
			}

			if y > height {
				break stat_loop
			}
			last_pos = y

			y_before   := y - (em / 2)
			y_after    := y_before
			next_line(&y_after, em)

			click_rect := rect(0, y_before, width, 2 * em)
			if pt_in_rect(mouse_pos, click_rect) {
				set_cursor("pointer")
			}

			if clicked && pt_in_rect(clicked_pos, click_rect) {
				selected_func = name
			}

			if selected_func.start == name.start {
				draw_rect(rects, click_rect, highlight_color)
			}

			cursor = x_subpad

			total_perc := (stat.total_time / total_tracked_time) * 100

			total_text := fmt.tprintf("%10s", stat_fmt(stat.total_time))
			total_perc_text := fmt.tprintf("%.1f%%", total_perc)

			self_text := fmt.tprintf("%10s", stat_fmt(stat.self_time))
			min_text := fmt.tprintf("%10s", stat_fmt(stat.min_time))
			avg_text := fmt.tprintf("%10s", stat_fmt(stat.avg_time))
			max_text := fmt.tprintf("%10s", stat_fmt(stat.max_time))

			text_outf(rects, &cursor, y, self_text, text_color2);   cursor += column_gap
			{
				full_perc_width := measure_text(total_perc_text, .PSize, .MonoFont)
				perc_width := (ch_width * 6) - full_perc_width

				text_outf(rects, &cursor, y, total_text, text_color2); cursor += ch_width
				cursor += perc_width
				draw_text(rects, total_perc_text, Vec2{cursor, y}, .PSize, .MonoFont, text_color2); cursor += column_gap + full_perc_width
			}

			text_outf(rects, &cursor, y, min_text, text_color2);   cursor += column_gap
			text_outf(rects, &cursor, y, avg_text, text_color2);   cursor += column_gap
			text_outf(rects, &cursor, y, max_text, text_color2);   cursor += column_gap

			dr := rect(cursor, y_before, (display_width - cursor - column_gap) * stat.total_time / full_time, y_after - y_before)
			cursor += column_gap / 2

			name_str := in_getstr(&trace.string_block, name)
			name_width := measure_text(name_str, .PSize, .MonoFont)
			tmp_color := trace.color_choices[name_color_idx(trace, name_str)]
			draw_rect(rects, dr, BVec4{u8(tmp_color.x), u8(tmp_color.y), u8(tmp_color.z), 255})
			draw_text(rects, name_str, Vec2{cursor, y_before + (em / 3)}, .PSize, .MonoFont, text_color)

			next_line(&y, em)
		}

		if selected_func.start != -1 {
			histogram_height := 250.0
			line_gap := (em / 1.5)
			edge_gap := (em / 2)
			pos := Vec2{
				(graph_rect.pos.x + graph_rect.size.x) - histogram_height - edge_gap,
				info_pane_y - histogram_height - ((em + line_gap) * 2) - edge_gap,
			}

			name_str := in_getstr(&trace.string_block, selected_func)
			stat, ok := sm_get(&trace.stats, selected_func)
			if ok {
				draw_histogram(rects, name_str, stat, pos, histogram_height)
			}
		}

		y = header_start
		cursor = 0

		draw_rect(rects, rect(0, info_pane_y, width, 2 * em), subbar_color)
		draw_line(rects, Vec2{0, info_pane_y + (2 * em)}, Vec2{width, info_pane_y + (2 * em)}, 1, line_color)

		column_header :: proc(rects: ^[dynamic]DrawRect, cursor: ^f64, column_gap, text_y, rect_y, pane_h: f64, text: string, sort_type: SortState) {
			start_x := cursor^
			cursor^ += (column_gap / 2)

			width := measure_text(text, .PSize, .MonoFont)
			draw_text(rects, text, Vec2{cursor^, text_y}, .PSize, .MonoFont, text_color)
			cursor^ += width + (column_gap / 2)
			end_x := cursor^

			if stat_sort_type == sort_type {
				arrow_icon := stat_sort_descending ? "\uf0dd" : "\uf0de"
				arrow_height := get_text_height(.PSize, .IconFont)
				arrow_width := measure_text(arrow_icon, .PSize, .IconFont)
				draw_text(rects, arrow_icon, Vec2{end_x - arrow_width - (column_gap / 2), rect_y + (em) - (arrow_height / 2)}, .PSize, .IconFont, text_color)
			}

			draw_line(rects, Vec2{cursor^, rect_y}, Vec2{cursor^, rect_y + pane_h}, 1, subbar_split_color)

			click_rect := rect(start_x, rect_y, end_x - start_x, 2 * em)
			if pt_in_rect(mouse_pos, click_rect) {
				set_cursor("pointer")
			}

			if clicked && pt_in_rect(clicked_pos, click_rect) {
				if stat_sort_type == sort_type {
					stat_sort_descending = !stat_sort_descending
				} else {
					stat_sort_type = sort_type
					stat_sort_descending = true
				}
				resort_stats = true
			}
		}

		self_header_text   := fmt.tprintf("%-10s", "   self")
		column_header(rects, &cursor, column_gap, y, info_pane_y, info_pane_height, self_header_text, .SelfTime)

		total_header_text  := fmt.tprintf("%-17s", "      total")
		column_header(rects, &cursor, column_gap, y, info_pane_y, info_pane_height, total_header_text, .TotalTime)

		min_header_text    := fmt.tprintf("%-10s", "   min.")
		column_header(rects, &cursor, column_gap, y, info_pane_y, info_pane_height, min_header_text, .MinTime)

		avg_header_text    := fmt.tprintf("%-10s", "   avg.")
		column_header(rects, &cursor, column_gap, y, info_pane_y, info_pane_height, avg_header_text, .AvgTime)

		max_header_text    := fmt.tprintf("%-10s", "   max.")
		column_header(rects, &cursor, column_gap, y, info_pane_y, info_pane_height, max_header_text, .MaxTime)

		name_header_text   := fmt.tprintf("%-10s", "   name")
		text_outf(rects, &cursor, y, name_header_text, text_color)
	} else {
		y := height - em - top_line_gap

		draw_text(rects, "Shift-click and drag to get stats for multiple rectangles", Vec2{x_subpad, prev_line(&y, em)}, .PSize, .DefaultFont, text_color)
		draw_text(rects, "Click on a rectangle to inspect", Vec2{x_subpad, prev_line(&y, em)}, .PSize, .DefaultFont, text_color)
	}
}

process_multiselect :: proc(rects: ^[dynamic]DrawRect, trace: ^Trace, pan_delta: Vec2, dt, info_pane_y, rect_height: f64) -> (just_started, render_one_more: bool) {
	// Handle single-select
	if mouse_up_now && !did_pan && pt_in_rect(clicked_pos, graph_rect) && pressed_event == released_event && !shift_down {
		selected_event = released_event
		clicked_on_rect = true
		did_multiselect = false
		render_one_more = true
	}

	// Handle de-select
	if mouse_up_now && !did_pan && pt_in_rect(clicked_pos, graph_rect) && !clicked_on_rect && !shift_down {
		selected_event = {-1, -1, -1, -1}
		resize(&trace.selected_ranges, 0)

		multiselect_t = 0
		did_multiselect = false
		stats_state = .NoStats
		render_one_more = true
	}

	// user wants to multi-select
	if is_mouse_down && shift_down {
		if !did_multiselect {
			multiselect_t = t
			anim_playing = true
		}

		// set multiselect flags
		stats_state = .Started
		did_multiselect = true
		total_tracked_time = 0.0
		cur_stat_offset = StatOffset{}
		selected_event = {-1, -1, -1, -1}
		info_pane_scroll = 0
		info_pane_scroll_vel = 0
		just_started = true

		// try to fake a reduced frame of latency by extrapolating the position by the delta
		mouse_pos_extrapolated := mouse_pos + 1 * Vec2{pan_delta.x, pan_delta.y} / dt * min(dt, 0.016)

		// cap multi-select box at graph edges
		delta := mouse_pos_extrapolated - clicked_pos
		c_x := min(clicked_pos.x, graph_rect.pos.x + graph_rect.size.x)
		c_x = max(c_x, graph_rect.pos.x)

		c_y := min(clicked_pos.y, graph_rect.pos.y + graph_rect.size.y)
		c_y = max(c_y, graph_rect.pos.y)

		m_x := min(c_x + delta.x, graph_rect.pos.x + graph_rect.size.x)
		m_x = max(m_x, graph_rect.pos.x)
		m_y := min(c_y + delta.y, graph_rect.pos.y + graph_rect.size.y)
		m_y = max(m_y, graph_rect.pos.y)

		d_x := m_x - c_x
		d_y := m_y - c_y

		// draw multiselect box
		selected_rect := rect(c_x, c_y, d_x, d_y)
		multiselect_color := toolbar_color
		draw_rect_inline(rects, selected_rect, 1, multiselect_color)
		multiselect_color.w = 20
		draw_rect(rects, selected_rect, multiselect_color)

		// transform multiselect rect to screen position
		flopped_rect := Rect{}
		flopped_rect.pos.x = min(selected_rect.pos.x, selected_rect.pos.x + selected_rect.size.x)
		x2 := max(selected_rect.pos.x, selected_rect.pos.x + selected_rect.size.x)
		flopped_rect.size.x = x2 - flopped_rect.pos.x

		flopped_rect.pos.y = min(selected_rect.pos.y, selected_rect.pos.y + selected_rect.size.y)
		y2 := max(selected_rect.pos.y, selected_rect.pos.y + selected_rect.size.y)
		flopped_rect.size.y = y2 - flopped_rect.pos.y

		selected_start_time := to_world_x(cam, flopped_rect.pos.x - disp_rect.pos.x)
		selected_end_time   := to_world_x(cam, flopped_rect.pos.x - disp_rect.pos.x + flopped_rect.size.x)

		// draw multiselect timerange
		width_text := measure_fmt(selected_end_time - selected_start_time)
		width_text_width := measure_text(width_text, .PSize, .MonoFont) + em

		text_bg_rect := flopped_rect
		text_bg_rect.pos.x = text_bg_rect.pos.x + (text_bg_rect.size.x / 2) - (width_text_width / 2)
		text_bg_rect.pos.y = text_bg_rect.pos.y - (p_height * 2)
		text_bg_rect.size.x = width_text_width
		text_bg_rect.size.y = (p_height * 2)

		if flopped_rect.size.x > text_bg_rect.size.x {
			multiselect_color.w = 180
			draw_rect(rects, text_bg_rect, multiselect_color)
			draw_text(rects,
				width_text, 
				Vec2{
					text_bg_rect.pos.x + (em / 2), 
					text_bg_rect.pos.y + (p_height / 2),
				}, 
				.PSize,
				.MonoFont,
				BVec4{255, 255, 255, 255},
			)
		}

		// push it into screen-space
		flopped_rect.pos.x -= disp_rect.pos.x

		sm_clear(&trace.stats)
		resize(&trace.selected_ranges, 0)

		// build out ranges
		cur_y := padded_graph_rect.pos.y - cam.pan.y
		proc_loop2: for proc_v, p_idx in trace.processes {
			h1_size : f64 = 0
			if len(trace.processes) > 1 {
				h1_size = h1_height + (h1_height / 2)
				cur_y += h1_size
			}

			for tm, t_idx in proc_v.threads {
				h2_size := h2_height + (h2_height / 2)
				cur_y += h2_size
				if cur_y > info_pane_y {
					break proc_loop2
				}

				thread_advance := ((f64(len(tm.depths)) * rect_height) + thread_gap)
				if cur_y + thread_advance < 0 {
					cur_y += thread_advance
					continue
				}

				for depth, d_idx in tm.depths {
					y := rect_height * f64(d_idx)
					h := rect_height

					dy := cur_y + y
					dy2 := cur_y + y + h
					if dy > (flopped_rect.pos.y + flopped_rect.size.y) || dy2 < flopped_rect.pos.y {
						continue
					}

					start_idx := find_idx(trace, depth.events[:], selected_start_time)
					end_idx := find_idx(trace, depth.events[:], selected_end_time)
					if start_idx == -1 {
						start_idx = 0
					}
					if end_idx == -1 {
						end_idx = len(depth.events) - 1
					}
					scan_arr := depth.events[start_idx:end_idx+1]

					real_start := -1
					fwd_scan_loop: for i := 0; i < len(scan_arr); i += 1 {
						ev := scan_arr[i]
						x := ev.timestamp - trace.total_min_time

						duration := bound_duration(ev, tm.max_time)
						w := duration * cam.current_scale

						r := Rect{Vec2{x, y}, Vec2{w, h}}
						r_x := (r.pos.x * cam.current_scale) + cam.pan.x
						r_y := cur_y + r.pos.y
						dr := Rect{Vec2{r_x, r_y}, Vec2{r.size.x, r.size.y}}

						if !rect_in_rect(flopped_rect, dr) {
							continue fwd_scan_loop
						}

						real_start = start_idx + i
						break fwd_scan_loop
					}

					real_end := -1
					rev_scan_loop: for i := len(scan_arr) - 1; i >= 0; i -= 1 {
						ev := scan_arr[i]
						x := ev.timestamp - trace.total_min_time

						duration := bound_duration(ev, tm.max_time)
						w := duration * cam.current_scale

						r := Rect{Vec2{x, y}, Vec2{w, h}}
						r_x := (r.pos.x * cam.current_scale) + cam.pan.x
						r_y := cur_y + r.pos.y
						dr := Rect{Vec2{r_x, r_y}, Vec2{r.size.x, r.size.y}}

						if !rect_in_rect(flopped_rect, dr) {
							continue rev_scan_loop
						}

						real_end = start_idx + i + 1
						break rev_scan_loop
					}

					if real_start != -1 && real_end != -1 {
						append(&trace.selected_ranges, Range{p_idx, t_idx, d_idx, real_start, real_end})
					}
				}
				cur_y += thread_advance
			}
		}
	}

	if stats_state == .Started && did_multiselect {
		event_count := 0
		iter_max := just_started ? INITIAL_ITER : FULL_ITER

		broke_early := false
		range_loop: for range, r_idx in trace.selected_ranges {
			start_idx := range.start
			if cur_stat_offset.range_idx > r_idx {
				continue
			} else if cur_stat_offset.range_idx == r_idx {
				start_idx = max(start_idx, cur_stat_offset.event_idx)
			}

			thread := trace.processes[range.pid].threads[range.tid]
			events := thread.depths[range.did].events[start_idx:range.end]

			for ev, e_idx in events {
/*
				if event_count > iter_max {
					cur_stat_offset = StatOffset{r_idx, start_idx + e_idx}
					broke_early = true
					break range_loop
				}
*/

				duration := bound_duration(ev, thread.max_time)
				name := in_getstr(&trace.string_block, ev.name)
				s, ok := sm_get(&trace.stats, ev.name)
				if !ok {
					s = sm_insert(&trace.stats, ev.name, Stats{min_time = 1e308})
				}

				s.count += 1
				s.total_time += duration
				s.self_time += ev.self_time
				s.min_time = min(s.min_time, duration)
				s.max_time = max(s.max_time, duration)
				total_tracked_time += duration

				event_count += 1
			}

			for ev, e_idx in events {
				duration := bound_duration(ev, thread.max_time)
				name := in_getstr(&trace.string_block, ev.name)
				s, ok := sm_get(&trace.stats, ev.name)
				assert(ok == true)

				assert(duration <= s.max_time)
				assert(s.max_time - s.min_time >= 0)
				if (s.max_time - s.min_time <= 0) {
					s.hist[50] += 1
				} else {
					t := (duration - s.min_time) / (s.max_time - s.min_time)
					t = min(1, max(t, 0))
					t *= 99

					assert(t < 100)
					s.hist[u32(t)] += 1
				}
			}
		}

		if !broke_early {
			for i := 0; i < len(trace.stats.entries); i += 1 {
				stat := &trace.stats.entries[i].val
				stat.avg_time = stat.total_time / f64(stat.count)
			}

			self_sort :: proc(a, b: StatEntry) -> bool {
				return a.val.self_time > b.val.self_time
			}
			sm_sort(&trace.stats, self_sort)
			stats_state = .Finished
		}
	}

	return
}

sort_stats :: proc(trace: ^Trace) {
	less: proc(a, b: StatEntry) -> bool
	switch stat_sort_type {
	case .SelfTime:
		less = proc(a, b: StatEntry) -> bool {
			if stat_sort_descending {
				return a.val.self_time > b.val.self_time
			} else {
				return a.val.self_time < b.val.self_time
			}
		}
	case .TotalTime:
		less = proc(a, b: StatEntry) -> bool {
			if stat_sort_descending {
				return a.val.total_time > b.val.total_time
			} else {
				return a.val.total_time < b.val.total_time
			}
		}
	case .MinTime:
		less = proc(a, b: StatEntry) -> bool {
			if stat_sort_descending {
				return a.val.min_time > b.val.min_time
			} else {
				return a.val.min_time < b.val.min_time
			}
		}
	case .AvgTime:
		less = proc(a, b: StatEntry) -> bool {
			if stat_sort_descending {
				return a.val.avg_time > b.val.avg_time
			} else {
				return a.val.avg_time < b.val.avg_time
			}
		}
	case .MaxTime:
		less = proc(a, b: StatEntry) -> bool {
			if stat_sort_descending {
				return a.val.max_time > b.val.max_time
			} else {
				return a.val.max_time < b.val.max_time
			}
		}
	}
	sm_sort(&trace.stats, less)
}

process_inputs :: proc(trace: ^Trace, stat_pane, mini_graph_rect: Rect, dt, display_width, rect_height, start_x: f64) -> (f64, f64, Vec2) {
	start_time, end_time: f64
	pan_delta: Vec2
	{
		old_scale := cam.target_scale

		max_scale := 10000000.0
		min_scale := 0.5 * display_width / (trace.total_max_time - trace.total_min_time)
		if pt_in_rect(mouse_pos, graph_rect) {
			cam.target_scale *= math.pow(1.0025, -scroll_val_y)
			cam.target_scale  = min(max(cam.target_scale, min_scale), max_scale)
		} else if pt_in_rect(mouse_pos, stat_pane) {
			info_pane_scroll_vel -= scroll_val_y * 10
		} else if pt_in_rect(mouse_pos, mini_graph_rect) {
			cam.vel.y += scroll_val_y * 10
		}
		scroll_val_y = 0

		info_pane_scroll += (info_pane_scroll_vel * dt)
		info_pane_scroll_vel *= math.pow(0.000001, dt)
		info_pane_scroll = min(info_pane_scroll, 0)

		cam.current_scale += (cam.target_scale - cam.current_scale) * (1 - math.pow(math.pow_f64(0.1, 12), (dt)))
		cam.current_scale = min(max(cam.current_scale, min_scale), max_scale)

		last_start_time, last_end_time := get_current_window(cam, display_width)

		get_max_y_pan :: proc(processes: []Process, rect_height: f64) -> f64 {
			cur_y : f64 = 0

			for proc_v, _ in processes {
				if len(processes) > 1 {
					h1_size := h1_height + (h1_height / 2)
					cur_y += h1_size
				}

				for tm, _ in proc_v.threads {
					h2_size := h2_height + (h2_height / 2)
					cur_y += h2_size + ((f64(len(tm.depths)) * rect_height) + thread_gap)
				}
			}

			return cur_y
		}
		max_height := get_max_y_pan(trace.processes[:], rect_height)
		max_y_pan := max(+20 * em + max_height - graph_rect.size.y, 0)
		min_y_pan := min(-20 * em, max_y_pan)
		max_x_pan := max(+20 * em, 0)
		min_x_pan := min(-20 * em + display_width + -(trace.total_max_time - trace.total_min_time) * cam.target_scale, max_x_pan)

		// compute pan, scale + scroll
		if is_mouse_down || mouse_up_now {
			MIN_PAN :: 5
			pan_dist := distance(mouse_pos, clicked_pos)
			if pan_dist > MIN_PAN {
				did_pan = true
			}
		}

		if did_pan {
			pan_delta = mouse_pos - last_mouse_pos
		}

		if is_mouse_down && !shift_down {
			if pt_in_rect(clicked_pos, padded_graph_rect) {

				if cam.target_pan_x < min_x_pan {
					pan_delta.x *= math.pow_f64(2, (cam.target_pan_x - min_x_pan) / 32)
				}
				if cam.target_pan_x > max_x_pan {
					pan_delta.x *= math.pow(2, (max_x_pan - cam.target_pan_x) / 32)
				}
				if cam.pan.y < min_y_pan {
					pan_delta.y *= math.pow(2, (cam.pan.y - min_y_pan) / 32)
				}
				if cam.pan.y > max_y_pan {
					pan_delta.y *= math.pow(2, (max_y_pan - cam.pan.y) / 32)
				}

				cam.vel.y = -pan_delta.y / dt
				cam.vel.x = pan_delta.x / dt
			}
			last_mouse_pos = mouse_pos
		}


		cam_mouse_x := mouse_pos.x - start_x

		if cam.target_scale != old_scale {
			cam.target_pan_x = ((cam.target_pan_x - cam_mouse_x) * (cam.target_scale / old_scale)) + cam_mouse_x
			if cam.target_pan_x < min_x_pan {
				cam.target_pan_x = min_x_pan
			}
			if cam.target_pan_x > max_x_pan {
				cam.target_pan_x = max_x_pan
			}
		}

		cam.target_pan_x = cam.target_pan_x + (cam.vel.x * dt)
		cam.pan.y = cam.pan.y + (cam.vel.y * dt)
		cam.vel *= math.pow(0.0001, dt)

		edge_sproing : f64 = 0.0001
		if cam.pan.y < min_y_pan && !is_mouse_down {
			cam.pan.y = min_y_pan + (cam.pan.y - min_y_pan) * math.pow(edge_sproing, dt)
			cam.vel.y *= math.pow(0.0001, dt)
		}
		if cam.pan.y > max_y_pan && !is_mouse_down {
			cam.pan.y = max_y_pan + (cam.pan.y - max_y_pan) * math.pow(edge_sproing, dt)
			cam.vel.y *= math.pow(0.0001, dt)
		}

		if cam.target_pan_x < min_x_pan && !is_mouse_down {
			cam.target_pan_x = min_x_pan + (cam.target_pan_x - min_x_pan) * math.pow(edge_sproing, dt)
			cam.vel.x *= math.pow(0.0001, dt)
		}
		if cam.target_pan_x > max_x_pan && !is_mouse_down {
			cam.target_pan_x = max_x_pan + (cam.target_pan_x - max_x_pan) * math.pow(edge_sproing, dt)
			cam.vel.x *= math.pow(0.0001, dt)
		}

		cam.pan.x = cam.target_pan_x + (cam.pan.x - cam.target_pan_x) * math.pow(math.pow_f64(0.1, 12.0), dt)
		start_time, end_time = get_current_window(cam, display_width)
	}

	return start_time, end_time, pan_delta
}
